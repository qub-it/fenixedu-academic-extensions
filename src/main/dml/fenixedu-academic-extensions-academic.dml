package org.fenixedu.academic.domain;

enum org.fenixedu.academic.domain.GradeScale as GradeScaleAcademicExtensions;
enum org.fenixedu.academic.domain.evaluation.markSheet.CompetenceCourseMarkSheetStateEnum as CompetenceCourseMarkSheetStateEnum;
enum org.fenixedu.academic.domain.evaluation.markSheet.CompetenceCourseMarkSheetChangeRequestStateEnum as CompetenceCourseMarkSheetChangeRequestStateEnum;
enum org.fenixedu.academic.domain.evaluation.season.EvaluationSeasonPeriodType as EvaluationSeasonPeriodType; 

valueType org.fenixedu.academic.domain.ShiftTypes as ShiftTypes {
    externalizeWith {
        JsonElement toJson();
    }
}

external class .org.fenixedu.bennu.io.domain.GenericFile;
external class .org.fenixedu.academic.domain.ExecutionDegree;

class EnrolmentType {
	String code (REQUIRED);
	LocalizedString name (REQUIRED);
}
	relation EnrolmentTypeRoot {
	    .org.fenixedu.bennu.core.domain.Bennu playsRole root { multiplicity 1..1; }
	    EnrolmentType playsRole enrolmentTypes { multiplicity *; }
	}

// ==================== BEGIN: Evaluations ====================

class evaluation.season.EvaluationSeasonPeriod {
	EvaluationSeasonPeriodType periodType;
	protected JsonElement intervalsRaw;
}


class evaluation.config.MarkSheetSettings {
	boolean allowTeacherToChooseCertifier;
	int requiredNumberOfShifts;
	boolean limitCertifierToResponsibleTeacher;
	boolean limitCreationToResponsibleTeacher;
}

relation MarkSheetSettingsBennuRoot {
	.org.fenixedu.bennu.core.domain.Bennu playsRole root { multiplicity 1..1; }
    evaluation.config.MarkSheetSettings playsRole markSheetSettings { multiplicity 0..1; }	
}


class evaluation.config.CompetenceCourseMarkSheetTemplateFile extends .org.fenixedu.bennu.io.domain.GenericFile {

}

relation CompetenceCourseMarkSheetTemplateFileMarkSheetSettings {

	evaluation.config.MarkSheetSettings playsRole markSheetSettings {
    	multiplicity 1..1;
    }
    
    evaluation.config.CompetenceCourseMarkSheetTemplateFile playsRole templateFile {
        multiplicity 0..1;
    }
}


relation EvaluationSeasonPeriodRelation {
    protected evaluation.season.EvaluationSeasonPeriod playsRole evaluationSeasonPeriod {
        multiplicity *;
    }

    .org.fenixedu.academic.domain.EvaluationSeason playsRole season {
        multiplicity 1;
    }
}

relation EvaluationSeasonOccupationPeriod {
    protected evaluation.season.EvaluationSeasonPeriod playsRole evaluationSeasonPeriod {
        multiplicity *;
    }

    .org.fenixedu.academic.domain.OccupationPeriod playsRole occupationPeriod {
        multiplicity 1;
    }
}

relation EvaluationSeasonPeriodExecutionSemester {
    evaluation.season.EvaluationSeasonPeriod playsRole evaluationSeasonPeriod {
        multiplicity *;
    }

    .org.fenixedu.academic.domain.ExecutionSemester playsRole executionSemester {
        multiplicity 1;
    }
}

relation EvaluationSeasonPeriodExecutionDegree {
	.org.fenixedu.academic.domain.ExecutionDegree playsRole executionDegrees { multiplicity *; }	
    evaluation.season.EvaluationSeasonPeriod playsRole evaluationSeasonPeriods { multiplicity *; }	
}

class evaluation.season.EvaluationSeasonInformation {
	Integer seasonOrder;
	boolean active;
	boolean requiresEnrolmentEvaluation;
	boolean supportsEmptyGrades;
	boolean supportsTeacherConfirmation;
	Integer minPastYearsToAcceptImprovementEnrolments;
	Integer maxPastYearsToAcceptImprovementEnrolments;	
}

relation EvaluationSeasonInformationRelation {
    evaluation.season.EvaluationSeasonInformation playsRole information {
        multiplicity 1..1;
    }

    .org.fenixedu.academic.domain.EvaluationSeason playsRole season {
        multiplicity 1..1;
    }
}

class evaluation.season.rule.EvaluationSeasonRule {
}

	class evaluation.season.rule.BlockingTreasuryEventInDebt extends evaluation.season.rule.EvaluationSeasonRule {
	}

	class evaluation.season.rule.PreviousSeasonEvaluation extends evaluation.season.rule.EvaluationSeasonRule {
	}

	class evaluation.season.rule.PreviousSeasonMinimumGrade extends evaluation.season.rule.EvaluationSeasonRule {
		Grade minimum;
	}

	class evaluation.season.rule.PreviousSeasonBlockingGrade extends evaluation.season.rule.EvaluationSeasonRule {
		Grade blocking;
	}

    class evaluation.season.rule.GradeScaleValidator extends evaluation.season.rule.EvaluationSeasonRule {
        GradeScale gradeScale;
        String gradeValues;
        LocalizedString ruleDescription;
        boolean appliesToCurriculumAggregatorEntry;
    }

    class evaluation.season.rule.EvaluationSeasonShiftType extends evaluation.season.rule.EvaluationSeasonRule {
        ShiftTypes shiftTypes (REQUIRED);
    }

    class evaluation.season.rule.EvaluationSeasonStatuteType extends evaluation.season.rule.EvaluationSeasonRule {
    }

relation EvaluationSeasonRuleRelation {
    evaluation.season.rule.EvaluationSeasonRule playsRole rules {
        multiplicity *;
    }

    .org.fenixedu.academic.domain.EvaluationSeason playsRole season {
        multiplicity 1;
    }
}

relation EvaluationSeasonStatuteTypeRelation {
    evaluation.season.rule.EvaluationSeasonStatuteType playsRole evaluationSeasonStatuteTypeRules {
        multiplicity *;
    }

    .org.fenixedu.academic.domain.student.StatuteType playsRole statuteTypes {
        multiplicity 1..*;
    }
}

relation GradeScaleValidatorDegreeTypes {
    evaluation.season.rule.GradeScaleValidator playsRole gradeScaleValidator {
        multiplicity *;
    }

    .org.fenixedu.academic.domain.degree.DegreeType playsRole degreeType {
        multiplicity 1..*;
    }
}

class evaluation.markSheet.CompetenceCourseMarkSheet {
    LocalDate evaluationDate;
    boolean printed;
    GradeScaleAcademicExtensions gradeScale;
    LocalDate expireDate;
}

class evaluation.markSheet.CompetenceCourseMarkSheetStateChange {
    CompetenceCourseMarkSheetStateEnum state;
    DateTime date;
    String reason;
    boolean byTeacher;
}

class evaluation.markSheet.CompetenceCourseMarkSheetSnapshot {
	String competenceCourseCode;
	LocalizedString competenceCourseName;
	String executionSemester;
	LocalizedString evaluationSeason;
	String certifier;
	LocalDate evaluationDate;
	DateTime evaluationDateTime;
	String checkSum;
	String shifts;
}

class evaluation.markSheet.CompetenceCourseMarkSheetSnapshotEntry {
    Integer studentNumber;
    String studentName;
    Grade grade;
    String degreeCode;
    LocalizedString degreeName;
    String shifts;
}

class evaluation.markSheet.CompetenceCourseMarkSheetChangeRequest {
    CompetenceCourseMarkSheetChangeRequestStateEnum state;
    String reason;
    DateTime requestDate;
    String comments;
    DateTime responseDate;
}

relation CompetenceCourseMarkSheetStateChangeSnapshot {
	evaluation.markSheet.CompetenceCourseMarkSheetSnapshot playsRole snapshot {
		multiplicity 0..1;
	}
	
	evaluation.markSheet.CompetenceCourseMarkSheetStateChange playsRole stateChange {
		multiplicity 1..1;
	}
}

relation CompetenceCourseMarkSheetSnapshotEntryRelation {
	protected evaluation.markSheet.CompetenceCourseMarkSheetSnapshot playsRole snapshot {
		multiplicity 1..1;
	}
	
	evaluation.markSheet.CompetenceCourseMarkSheetSnapshotEntry playsRole entry {
		multiplicity 1..*;
	}
}

relation CompetenceCourseMarkSheetStateChangeRelation {
	evaluation.markSheet.CompetenceCourseMarkSheet playsRole competenceCourseMarkSheet {
		multiplicity 1..1;
	}
	
	evaluation.markSheet.CompetenceCourseMarkSheetStateChange playsRole stateChange {
		multiplicity 1..*;
	}
}

relation CompetenceCourseMarkSheetStateChangeResponsible {
    protected evaluation.markSheet.CompetenceCourseMarkSheetStateChange playsRole competenceCourseMarkSheetChange {
        multiplicity *;
    }
    
    .org.fenixedu.academic.domain.Person playsRole responsible {
		multiplicity 1..1;
	}
}

relation CompetenceCourseMarkSheetRelation {
    evaluation.markSheet.CompetenceCourseMarkSheet playsRole competenceCourseMarkSheet {
        multiplicity *;
    }
    
    .org.fenixedu.academic.domain.CompetenceCourse playsRole competenceCourse {
		multiplicity 1..1;
	}
}

relation CompetenceCourseMarkSheetExecutionCourse {
    evaluation.markSheet.CompetenceCourseMarkSheet playsRole competenceCourseMarkSheet {
        multiplicity *;
    }
    
    .org.fenixedu.academic.domain.ExecutionCourse playsRole executionCourse {
		multiplicity 0..1;
	}
}

relation CompetenceCourseMarkSheetShift {
    protected evaluation.markSheet.CompetenceCourseMarkSheet playsRole competenceCourseMarkSheet {
        multiplicity *;
    }
    
    .org.fenixedu.academic.domain.Shift playsRole shift {
		multiplicity *;
	}
}

relation CompetenceCourseMarkSheetEnrolmentEvaluation {
    evaluation.markSheet.CompetenceCourseMarkSheet playsRole competenceCourseMarkSheet {
		multiplicity 0..1;
	}
    
    .org.fenixedu.academic.domain.EnrolmentEvaluation playsRole enrolmentEvaluation {
        multiplicity *;
    }
}

relation CompetenceCourseMarkSheetEvaluation {
    evaluation.markSheet.CompetenceCourseMarkSheet playsRole competenceCourseMarkSheet {
        multiplicity *;
    }

    .org.fenixedu.academic.domain.Evaluation playsRole courseEvaluation {
        multiplicity 0..1;
    }
}

relation CompetenceCourseMarkSheetEvaluationSeason {
    protected evaluation.markSheet.CompetenceCourseMarkSheet playsRole competenceCourseMarkSheet {
        multiplicity *;
    }
    
    .org.fenixedu.academic.domain.EvaluationSeason playsRole evaluationSeason {
        multiplicity 1..1;
    }
}

relation CompetenceCourseMarkSheetExecutionSemester {
    .org.fenixedu.academic.domain.ExecutionSemester playsRole executionSemester {
        multiplicity 1..1;
    }
    
    evaluation.markSheet.CompetenceCourseMarkSheet playsRole competenceCourseMarkSheet {
        multiplicity *;
    }
}

relation CompetenceCourseMarkSheetCertifier {
    evaluation.markSheet.CompetenceCourseMarkSheet playsRole competenceCourseMarkSheet {
        multiplicity *;
    }

    .org.fenixedu.academic.domain.Person playsRole certifier {
		multiplicity 1..1;
	}
}

relation CompetenceCourseMarkSheetCompetenceCourseMarkSheetChangeRequest {

	evaluation.markSheet.CompetenceCourseMarkSheet playsRole competenceCourseMarkSheet {
        multiplicity 1..1;
    }
    
	evaluation.markSheet.CompetenceCourseMarkSheetChangeRequest playsRole changeRequests {
		multiplicity *;
	}
	
}

relation CompetenceCourseMarkSheetChangeRequestRequester {

	.org.fenixedu.academic.domain.Person playsRole requester {
        multiplicity 1..1;
    }
    
	evaluation.markSheet.CompetenceCourseMarkSheetChangeRequest playsRole requestedCompetenceCourseMarkSheetChangeRequests {
		multiplicity *;
	}
	
}

relation CompetenceCourseMarkSheetChangeRequestResponder {

	.org.fenixedu.academic.domain.Person playsRole responder {
        multiplicity 0..1;
    }
    
	evaluation.markSheet.CompetenceCourseMarkSheetChangeRequest playsRole respondedCompetenceCourseMarkSheetChangeRequests {
		multiplicity *;
	}
	
}

/* Evaluation Configuration */
class evaluation.CustomEvaluationConfiguration extends .org.fenixedu.academic.domain.EvaluationConfiguration {

}

class evaluation.EnrolmentEvaluationExtendedInformation {
	String remarks;
}

relation BennuEnrolmentEvaluationExtendedInformation {
	evaluation.EnrolmentEvaluationExtendedInformation playsRole enrolmentEvaluationExtendedInformations {
		multiplicity *;
	}
	
	protected .org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
        multiplicity 1..1;
    }
}

relation EnrolmentEvaluationEnrolmentEvaluationExtendedInformation {
    .org.fenixedu.academic.domain.EnrolmentEvaluation playsRole enrolmentEvaluation {
    	multiplicity 1..1;
    }
    
    evaluation.EnrolmentEvaluationExtendedInformation playsRole extendedInformation;
}

// ==================== END: Evaluations ====================

// ==================== BEGIN: Student Mobility ====================

class student.mobility.MobilityProgramType {
	String code;
	LocalizedString name;
	boolean active;
}
	
	relation MobilityProgramTypeBennu {
		.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
			multiplicity 1..1;
		}
	
		student.mobility.MobilityProgramType playsRole mobilityProgramTypes {
			multiplicity *;
		}
	}

class student.mobility.MobilityActivityType {
	String code;
	LocalizedString name;
	boolean active;
}

	relation MobilityActivityTypeBennu {
		.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
			multiplicity 1..1;
		}
	
		student.mobility.MobilityActivityType playsRole mobilityActivityTypes {
			multiplicity *;
		}
	}


class student.mobility.MobilityScientificArea {
	String code;
	LocalizedString name;
}

	relation MobilityScientificAreaBennu {
		.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
			multiplicity 1..1;
		}
	
		student.mobility.MobilityScientificArea playsRole mobilityScientificAreas {
			multiplicity *;
		}
	}

class student.mobility.MobilityProgrammeLevel {
	String code;
	LocalizedString name;
	boolean otherLevel;
}

	relation MobilityProgrammeLevelBennu {
		.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
			multiplicity 1..1;
		}
	
		student.mobility.MobilityProgrammeLevel playsRole mobilityProgrammeLevels {
			multiplicity *;
		}
	}

class student.mobility.MobilityRegistrationInformation {
	//The student can be either incoming, external student in mobility program
	// Or can be an internal student in mobility program
	boolean incoming;
	SchoolPeriodDuration programDuration;
	String otherOriginMobilityProgrammeLevel;
	String otherIncomingMobilityProgrammeLevel;
	boolean degreeBased;
	LocalDate beginDate;
	LocalDate endDate;
	String remarks;
	boolean national;
	boolean mainInformation;
}

	relation MobilityRegistrationInformationBennu {
		.org.fenixedu.bennu.core.domain.Bennu playsRole bennu {
			multiplicity 1..1;
		}
	
		student.mobility.MobilityRegistrationInformation playsRole mobilityRegistrationInformations {
			multiplicity *;
		}
	}

	relation MobilityRegistrationInformationMobilityProgramType {
		student.mobility.MobilityRegistrationInformation playsRole mobilityRegistrationInformations {
			multiplicity *;
		}
	
		student.mobility.MobilityProgramType playsRole mobilityProgramType;
	}
	
	relation MobilityRegistrationInformationMobilityActivityType {
		student.mobility.MobilityRegistrationInformation playsRole mobilityRegistrationInformations {
			multiplicity *;
		}
	
		student.mobility.MobilityActivityType playsRole mobilityActivityType;
	}
	
	relation MobilityRegistrationInformationRegistration {
	
		student.mobility.MobilityRegistrationInformation playsRole mobilityRegistrationInformations {
			multiplicity *;
		}
	
		.org.fenixedu.academic.domain.student.Registration playsRole registration {
			multiplicity 1..1;
		}
	}
	
	relation MobilityRegistrationInformationBeginExecutionSemester {
		
		student.mobility.MobilityRegistrationInformation playsRole {
			multiplicity *;
		}
		
		.org.fenixedu.academic.domain.ExecutionSemester playsRole begin;
	}
	
	relation MobilityRegistrationInformationEndExecutionSemester {
		
		student.mobility.MobilityRegistrationInformation playsRole mobilityRegistrationInformations {
			multiplicity *;
		}
	
		.org.fenixedu.academic.domain.ExecutionSemester playsRole end;
	}
	
	relation MobilityRegistrationInformationForeignInstitutionUnit {
		student.mobility.MobilityRegistrationInformation playsRole mobilityRegistrationInformations {
			multiplicity *;
		}
	
		.org.fenixedu.academic.domain.organizationalStructure.Unit playsRole foreignInstitutionUnit {
			multiplicity 0..1;
		}
	}
	
	relation MobilityRegistrationInformationMobilityScientificArea {
		student.mobility.MobilityRegistrationInformation playsRole mobilityRegistrationInformations {
			multiplicity *;
		}
	
		student.mobility.MobilityScientificArea playsRole mobilityScientificArea {
			multiplicity 0..1;
		}
	}
	
	relation MobilityRegistrationInformationMobilityProgrammeLevel {
		student.mobility.MobilityRegistrationInformation playsRole mobilityRegistrationInformations {
			multiplicity *;
		}
	
		student.mobility.MobilityProgrammeLevel playsRole incomingMobilityProgrammeLevel {
			multiplicity 0..1;
		}
	}
	
	relation MobilityRegistrationInformationOriginMobilityProgrammeLevel {
		student.mobility.MobilityRegistrationInformation playsRole mobilityRegistrationInformationsForOrigin {
			multiplicity *;
		}
	
		student.mobility.MobilityProgrammeLevel playsRole originMobilityProgrammeLevel {
			multiplicity 0..1;
		}
	}
	
	relation MobilityRegistrationInformationDegree {
		student.mobility.MobilityRegistrationInformation playsRole mobilityRegistrationInformations {
			multiplicity *;
		}
	
		.org.fenixedu.academic.domain.Degree playsRole degree {
			multiplicity 0..1;
		}
	}
	
	relation MobilityRegistrationInformationDegreeCurricularPlan {
		student.mobility.MobilityRegistrationInformation playsRole mobilityRegistrationInformations {
			multiplicity *;
		}
	
		.org.fenixedu.academic.domain.DegreeCurricularPlan playsRole degreeCurricularPlan {
			multiplicity 0..1;
		}
	}
	
	relation MobilityRegistrationInformationBranch {
		student.mobility.MobilityRegistrationInformation playsRole mobilityRegistrationInformations {
			multiplicity *;
		}
	
		.org.fenixedu.academic.domain.degreeStructure.CourseGroup playsRole branchCourseGroup {
			multiplicity 0..1;
		}
	}
	
	relation MobilityRegistrationInformationCountryUnit {
		student.mobility.MobilityRegistrationInformation playsRole mobilityRegistrationInformations {
			multiplicity *;
		}
	
	
		.org.fenixedu.academic.domain.organizationalStructure.CountryUnit playsRole countryUnit {
			multiplicity 0..1;
		}
	}
	
// ==================== END: Student Mobility ====================


class student.RegistrationDataByExecutionInterval  {
	Integer schoolClassEnrolmentPriority;
}
	relation RegistrationDataByExecutionIntervalExecutionInterval {
	    student.RegistrationDataByExecutionInterval playsRole registrationDataByExecutionIntervals { multiplicity *; }
	    .org.fenixedu.academic.domain.ExecutionInterval playsRole executionInterval;
	}
	relation RegistrationDataByExecutionIntervalRegistration {
	    student.RegistrationDataByExecutionInterval playsRole registrationDataByExecutionIntervals { multiplicity *; }
	    .org.fenixedu.academic.domain.student.Registration playsRole registration;
	}
	relation RegistrationDataByExecutionIntervalRootDomainObject {
	    student.RegistrationDataByExecutionInterval playsRole registrationDataByExecutionIntervals { multiplicity *; }
	    .org.fenixedu.bennu.core.domain.Bennu playsRole rootDomainObject;
	}

class enrolment.schoolClass.SchoolClassEnrolmentPreference {
	Integer preferenceOrder;
	DateTime lastModificationDate;
}
	relation SchoolClassEnrolmentPreferenceRegistrationDataByExecutionInterval {
	    enrolment.schoolClass.SchoolClassEnrolmentPreference playsRole schoolClassEnrolmentPreferences { multiplicity *; }
	    student.RegistrationDataByExecutionInterval playsRole registrationDataByExecutionInterval;
	}
	relation SchoolClassEnrolmentPreferenceSchoolClass {
	    enrolment.schoolClass.SchoolClassEnrolmentPreference playsRole schoolClassEnrolmentPreferences { multiplicity *; }
	    .org.fenixedu.academic.domain.SchoolClass playsRole schoolClass;
	}	
	relation SchoolClassEnrolmentPreferenceRootDomainObject {
		enrolment.schoolClass.SchoolClassEnrolmentPreference playsRole schoolClassEnrolmentPreferences { multiplicity *; }
	    .org.fenixedu.bennu.core.domain.Bennu playsRole rootDomainObject;
	}
		
class researchPublication.ResearchPublicationType {
	String code;
	LocalizedString name;
}
    relation ResearchPublicationTypeRootDomainObject {
        researchPublication.ResearchPublicationType playsRole researchPublicationTypes { multiplicity *; }
        .org.fenixedu.bennu.core.domain.Bennu playsRole root { multiplicity 1..1; }
    }
class researchPublication.ResearchPublication {
    String title;
    String authors;
    String publicationData;
    Integer year;
    Integer relevanceOrder;
}
    relation ResearchPublicationPerson {
        researchPublication.ResearchPublication playsRole researchPublications { multiplicity *; }
        .org.fenixedu.academic.domain.Person playsRole person { multiplicity 1..1; }
    }
    relation ResearchPublicationResearchPublicationType {
        researchPublication.ResearchPublication playsRole researchPublications { multiplicity *; }
        researchPublication.ResearchPublicationType playsRole type { multiplicity 1..1; }
    }    
    relation ResearchPublicationRootDomainObject {
        researchPublication.ResearchPublication playsRole researchPublications { multiplicity *; }
        .org.fenixedu.bennu.core.domain.Bennu playsRole root { multiplicity 1..1; }
    }

class person.JobType {
	String code;
	LocalizedString name;
}    
    relation JobTypeRoot {
        person.JobType playsRole jobTypes { multiplicity *; }
        .org.fenixedu.bennu.core.domain.Bennu playsRole root { multiplicity 1..1; }
    }
    relation JobTypeJob {
        .org.fenixedu.academic.domain.Job playsRole jobs { multiplicity *; }
        person.JobType playsRole type { multiplicity 1..1; }
    }    


relation QualificationInstitutionUnit {
    .org.fenixedu.academic.domain.Qualification playsRole qualificationsForInstitution { multiplicity *; }
    .org.fenixedu.academic.domain.organizationalStructure.Unit playsRole institutionUnit { multiplicity 0..1; }
}        
relation QualificationDegreeUnit {
    .org.fenixedu.academic.domain.Qualification playsRole qualificationsForDegree { multiplicity *; }
    .org.fenixedu.academic.domain.organizationalStructure.Unit playsRole degreeUnit { multiplicity 0..1; }
}

class person.qualifications.QualificationLevel {
	String code;
	LocalizedString name;
}
    relation QualificationLevelRoot {
        person.qualifications.QualificationLevel playsRole qualificationLevels { multiplicity *; }
        .org.fenixedu.bennu.core.domain.Bennu playsRole root { multiplicity 1..1; }
    }
    relation QualificationLevelQualification {
        person.qualifications.QualificationLevel playsRole level { multiplicity 0..1; }
        .org.fenixedu.academic.domain.Qualification playsRole qualifications { multiplicity *; }
    }    

class person.qualifications.QualificationLevelType {
	String code;
	LocalizedString name;
}
    relation QualificationLevelTypeRoot {
        person.qualifications.QualificationLevelType playsRole qualificationLevelTypes { multiplicity *; }
        .org.fenixedu.bennu.core.domain.Bennu playsRole root { multiplicity 1..1; }
    }
    relation QualificationLevelTypeQualificationLevel {
        person.qualifications.QualificationLevelType playsRole types { multiplicity *; }
        person.qualifications.QualificationLevel playsRole levels { multiplicity *; }
    }    
    relation QualificationLevelTypeQualification {
		person.qualifications.QualificationLevelType playsRole qualificationTypes { multiplicity *; }        
        .org.fenixedu.academic.domain.Qualification playsRole qualifications { multiplicity *; }
    }        

class person.qualifications.QualificationLevelTypeFamily {
	String code;
	LocalizedString name;
}
    relation QualificationLevelTypeFamilyRoot {
        person.qualifications.QualificationLevelTypeFamily playsRole qualificationLevelTypeFamilies { multiplicity *; }
        .org.fenixedu.bennu.core.domain.Bennu playsRole root { multiplicity 1..1; }
    }
    relation QualificationLevelTypeFamilyQualificationLevelType {
	    person.qualifications.QualificationLevelTypeFamily playsRole family { multiplicity 1..1; }
        person.qualifications.QualificationLevelType playsRole types { multiplicity *; }
    }    
    
class academicStructure.AcademicArea {
	String code;
	LocalizedString name;
}
    relation AcademicAreaRoot {
        academicStructure.AcademicArea playsRole academicAreas { multiplicity *; }
        .org.fenixedu.bennu.core.domain.Bennu playsRole root { multiplicity 1..1; }
    }
    relation AcademicAreaQualification {
		academicStructure.AcademicArea playsRole academicAreas { multiplicity *; }        
        .org.fenixedu.academic.domain.Qualification playsRole qualifications { multiplicity *; }
    }        
    relation AcademicAreaUnit {
		academicStructure.AcademicArea playsRole academicAreas { multiplicity *; }        
        .org.fenixedu.academic.domain.organizationalStructure.Unit playsRole units { multiplicity *; }
    }                            
    
class academicStructure.AcademicAreaType {
	String code;
	LocalizedString name;
}
    relation AcademicAreaTypeRoot {
        academicStructure.AcademicAreaType playsRole academicAreaTypes { multiplicity *; }
        .org.fenixedu.bennu.core.domain.Bennu playsRole root { multiplicity 1..1; }
    }
    relation AcademicAreaTypeAcademicArea {
        academicStructure.AcademicArea playsRole areas { multiplicity *; }
        academicStructure.AcademicAreaType playsRole type { multiplicity 1..1; }
    }  
    

// ==================== BEGIN: DynamicField ====================

class dml.DynamicFieldDescriptor {
	String domainObjectClassName (REQUIRED);
	String code (REQUIRED);
	LocalizedString name (REQUIRED);
	String fieldValueClassName (REQUIRED);
	boolean required;
	Integer minLength;
	Integer maxLength;
	BigDecimal minNumber;
	BigDecimal maxNumber;
	boolean largeSize;
	boolean richText;
	int order;
} 

	relation DynamicFieldDescriptorRoot {
		.org.fenixedu.bennu.core.domain.Bennu playsRole root { multiplicity 1..1; }
		dml.DynamicFieldDescriptor playsRole dynamicFieldDescriptor { multiplicity *; }
	}

class dml.DynamicField {
	String value;
} 

    relation DynamicFieldsDescriptors {
        dml.DynamicField playsRole instance { multiplicity *; }
        dml.DynamicFieldDescriptor playsRole descriptor { multiplicity 1..1; }
    }    
	
	relation DynamicFieldRoot {
		dml.DynamicField playsRole dynamicField { multiplicity *; }
		.org.fenixedu.bennu.core.domain.Bennu playsRole root { multiplicity 1..1; }
	}

	relation DynamicFieldsForCompetenceCourseInformation {
		dml.DynamicField playsRole dynamicField { multiplicity *; }
		.org.fenixedu.academic.domain.degreeStructure.CompetenceCourseInformation playsRole competenceCourseInformation { multiplicity 0..1; }
	}

	relation DynamicFieldsForDegreeInfo {
		dml.DynamicField playsRole dynamicField { multiplicity *; }
		.org.fenixedu.academic.domain.DegreeInfo playsRole degreeInfo { multiplicity 0..1; }
	}
	
	relation DynamicFieldsForParty {
		dml.DynamicField playsRole dynamicField { multiplicity *; }
		.org.fenixedu.academic.domain.organizationalStructure.Party playsRole party { multiplicity 0..1; }
	}

// ==================== END: DynamicField ====================



relation RegistrationSchoolClass {
    .org.fenixedu.academic.domain.SchoolClass playsRole schoolClasses {
        multiplicity *;
    }
    .org.fenixedu.academic.domain.student.Registration playsRole registrations {
        multiplicity *;
    }
}